# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/notification/distributed_notification_service/notification.gni")
import("//build/ohos.gni")

group("anm_target") {
  deps = [ ":anm" ]
}

config("tools_dump_config") {
  include_dirs = [ "${tools_path}/dump/include" ]
}

ohos_executable("anm") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  public_configs = [ ":tools_dump_config" ]

  sources = [
    "src/main.cpp",
    "src/notification_shell_command.cpp",
    "src/shell_command.cpp",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  deps = [ "${frameworks_module_ans_path}:ans_innerkits" ]

  configs = [ "${frameworks_module_ans_path}:ans_innerkits_public_config" ]

  defines = []
  if (distributed_notification_service_feature_distributed_db) {
    defines += [ "DISTRIBUTED_NOTIFICATION_SUPPORTED" ]
  }

  install_enable = false
  if (build_variant == "root") {
    defines += [ "ANM_SUPPORT_DUMP" ]
    install_enable = true
  }

  external_deps = [
    "ability_base:configuration",
    "access_token:libnativetoken_shared",
    "access_token:libtokensetproc_shared",
    "c_utils:utils",
    "ffrt:libffrt",
    "hilog:libhilog",
    "image_framework:image_native",
    "relational_store:native_rdb",
  ]
  external_deps += component_external_deps

  part_name = "${component_name}"
  subsystem_name = "${subsystem_name}"
}
