# Copyright (c) 2021-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/notification/distributed_notification_service/notification.gni")
import("//build/config/components/idl_tool/idl.gni")

idl_gen_interface("ans_manager_interface") {
  sources = [
    "IAnsDialogCallback.idl",
    "IAnsManager.idl",
    "IAnsOperationCallback.idl",
    "IAnsResultDataSynchronizer.idl",
    "IAnsSubscriber.idl",
    "IAnsSubscriberLocalLiveView.idl",
    "IBadgeQueryCallback.idl",
    "ISwingCallBack.idl",
  ]
  log_domainid = "0xD001203"
  log_tag = "Ans"
  subsystem_name = "notification"
  part_name = "distributed_notification_service"
}

group("ans_client") {
  deps = [ ":ans_innerkits" ]
}

config("ans_innerkits_public_config") {
  visibility = [ "./../../*" ]

  include_dirs = [
    "${inner_api_path}",
    "${interfaces_path}/kits/native",
    "${core_path}/common/include",
    "${core_path}/include",
    "${target_gen_dir}",
  ]
}

config("ans_innerkits_config") {
  visibility = [ "./../../*" ]

  include_dirs = [
    "${inner_api_path}",
    "${interfaces_path}/kits/native",
    "${core_path}/common/include",
    "${core_path}/include",
    "${target_gen_dir}",
  ]
}

ohos_shared_library("ans_innerkits") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
  version_script = "libans_innerkits.map"

  include_dirs = [ "${inner_api_path}" ]

  sources = [
    "${core_path}/common/src/ans_const_define.cpp",
    "${core_path}/common/src/ans_convert_enum.cpp",
    "${core_path}/common/src/ans_inner_errors.cpp",
    "${core_path}/common/src/ans_log_wrapper.cpp",
    "${core_path}/common/src/ans_permission_def.cpp",
    "${core_path}/src/ans_image_util.cpp",
    "${core_path}/src/ans_notification.cpp",
    "${core_path}/src/ans_result_data_synchronizer.cpp",
    "${core_path}/src/dialog_status_data.cpp",
    "${core_path}/src/hitrace_util.cpp",
    "${core_path}/src/listener/ans_subscriber_listener.cpp",
    "${core_path}/src/listener/badgequery_callback_listener.cpp",
    "${core_path}/src/listener/swing_callback_service.cpp",
    "${core_path}/src/manager/ans_manager_death_recipient.cpp",
    "${frameworks_module_ans_path}/src/ans_dialog_host_client.cpp",
    "${frameworks_module_ans_path}/src/badge_number_callback_data.cpp",
    "${frameworks_module_ans_path}/src/distributed_bundle_option.cpp",
    "${frameworks_module_ans_path}/src/enabled_notification_callback_data.cpp",
    "${frameworks_module_ans_path}/src/enabled_priority_notification_by_bundle_callback_data.cpp",
    "${frameworks_module_ans_path}/src/enabled_silent_reminder_callback_data.cpp",
    "${frameworks_module_ans_path}/src/message_user.cpp",
    "${frameworks_module_ans_path}/src/notification.cpp",
    "${frameworks_module_ans_path}/src/notification_action_button.cpp",
    "${frameworks_module_ans_path}/src/notification_basic_content.cpp",
    "${frameworks_module_ans_path}/src/notification_bundle_option.cpp",
    "${frameworks_module_ans_path}/src/notification_button_option.cpp",
    "${frameworks_module_ans_path}/src/notification_capsule.cpp",
    "${frameworks_module_ans_path}/src/notification_check_info.cpp",
    "${frameworks_module_ans_path}/src/notification_check_request.cpp",
    "${frameworks_module_ans_path}/src/notification_constant.cpp",
    "${frameworks_module_ans_path}/src/notification_content.cpp",
    "${frameworks_module_ans_path}/src/notification_conversational_content.cpp",
    "${frameworks_module_ans_path}/src/notification_conversational_message.cpp",
    "${frameworks_module_ans_path}/src/notification_disable.cpp",
    "${frameworks_module_ans_path}/src/notification_distributed_options.cpp",
    "${frameworks_module_ans_path}/src/notification_do_not_disturb_date.cpp",
    "${frameworks_module_ans_path}/src/notification_do_not_disturb_profile.cpp",
    "${frameworks_module_ans_path}/src/notification_extension_subscription_info.cpp",
    "${frameworks_module_ans_path}/src/notification_flags.cpp",
    "${frameworks_module_ans_path}/src/notification_geofence.cpp",
    "${frameworks_module_ans_path}/src/notification_helper.cpp",
    "${frameworks_module_ans_path}/src/notification_icon_button.cpp",
    "${frameworks_module_ans_path}/src/notification_live_view_content.cpp",
    "${frameworks_module_ans_path}/src/notification_local_live_view_button.cpp",
    "${frameworks_module_ans_path}/src/notification_local_live_view_content.cpp",
    "${frameworks_module_ans_path}/src/notification_local_live_view_subscriber.cpp",
    "${frameworks_module_ans_path}/src/notification_long_text_content.cpp",
    "${frameworks_module_ans_path}/src/notification_media_content.cpp",
    "${frameworks_module_ans_path}/src/notification_multiline_content.cpp",
    "${frameworks_module_ans_path}/src/notification_normal_content.cpp",
    "${frameworks_module_ans_path}/src/notification_operation_info.cpp",
    "${frameworks_module_ans_path}/src/notification_picture_content.cpp",
    "${frameworks_module_ans_path}/src/notification_progress.cpp",
    "${frameworks_module_ans_path}/src/notification_reminder_info.cpp",
    "${frameworks_module_ans_path}/src/notification_request.cpp",
    "${frameworks_module_ans_path}/src/notification_ringtone_info.cpp",
    "${frameworks_module_ans_path}/src/notification_slot.cpp",
    "${frameworks_module_ans_path}/src/notification_sorting.cpp",
    "${frameworks_module_ans_path}/src/notification_sorting_map.cpp",
    "${frameworks_module_ans_path}/src/notification_subscribe_info.cpp",
    "${frameworks_module_ans_path}/src/notification_subscriber.cpp",
    "${frameworks_module_ans_path}/src/notification_template.cpp",
    "${frameworks_module_ans_path}/src/notification_time.cpp",
    "${frameworks_module_ans_path}/src/notification_trigger.cpp",
    "${frameworks_module_ans_path}/src/notification_unified_group_info.cpp",
    "${frameworks_module_ans_path}/src/notification_user_input.cpp",
    "${frameworks_module_ans_path}/src/push_callback_stub.cpp",
    "${frameworks_module_ans_path}/src/push_promise_callback.cpp",
  ]

  public_configs = [ ":ans_innerkits_public_config" ]

  defines = []

  deps = [
    ":ans_callback_stub",
    ":ans_manager_proxy",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:appkit_native",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "cJSON:cjson_static",
    "c_utils:utils",
    "ffrt:libffrt",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "hitrace:libhitracechain",
    "i18n:intl_util",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
    "time_service:time_client",
  ]

  public_external_deps = [
    "ability_runtime:wantagent_innerkits",
    "image_framework:image_native",
    "json:nlohmann_json_static",
  ]

  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }

  if (build_variant == "root") {
    defines += [ "ANM_SUPPORT_DUMP" ]
  }

  if (ans_hitrace_usage) {
    external_deps += [ "hitrace:hitrace_meter" ]
    defines += [ "HITRACE_METER_ENABLE" ]
  }

  if (notification_smart_reminder_supported) {
    defines += [ "NOTIFICATION_SMART_REMINDER_SUPPORTED" ]
  }

  if (distributed_notification_service_feature_privileged_message) {
    defines += [ "ENABLE_ANS_PRIVILEGED_MESSAGE_EXT_WRAPPER" ]
  }

  if (distributed_notification_service_feature_priority_notification) {
    defines += [ "ANS_FEATURE_PRIORITY_NOTIFICATION" ]
  }

  subsystem_name = "${subsystem_name}"
  part_name = "${component_name}"
}

ohos_source_set("ans_manager_stub") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":ans_innerkits_config" ]
  output_values = get_target_outputs(":ans_manager_interface")
  sources = filter_include(output_values, [ "*ans_manager_stub.cpp" ])
  deps = [ ":ans_manager_interface" ]
  defines = []
  if (notification_smart_reminder_supported) {
    defines += [ "NOTIFICATION_SMART_REMINDER_SUPPORTED" ]
  }
  if (build_variant == "root") {
    defines += [ "ANM_SUPPORT_DUMP" ]
  }
  external_deps = [
    "ability_runtime:wantagent_innerkits",
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "hilog:libhilog",
    "i18n:intl_util",
    "image_framework:image_native",
    "ipc:ipc_core",
  ]
  subsystem_name = "notification"
  part_name = "distributed_notification_service"
}

ohos_source_set("ans_manager_proxy") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":ans_innerkits_config" ]
  output_values = get_target_outputs(":ans_manager_interface")
  sources = filter_include(output_values, [ "*ans_manager_proxy.cpp" ])
  deps = [ ":ans_manager_interface" ]
  defines = []
  if (notification_smart_reminder_supported) {
    defines += [ "NOTIFICATION_SMART_REMINDER_SUPPORTED" ]
  }
  if (build_variant == "root") {
    defines += [ "ANM_SUPPORT_DUMP" ]
  }
  external_deps = [
    "ability_runtime:wantagent_innerkits",
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "hilog:libhilog",
    "i18n:intl_util",
    "image_framework:image_native",
    "ipc:ipc_core",
  ]
  subsystem_name = "notification"
  part_name = "distributed_notification_service"
}

ohos_source_set("ans_callback_proxy") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":ans_innerkits_config" ]
  output_values = get_target_outputs(":ans_manager_interface")
  sources = filter_include(output_values,
                            [
                              "*_dialog_callback_proxy.cpp",
                              "*_operation_callback_proxy.cpp",
                              "*_result_data_synchronizer_proxy.cpp",
                              "*_subscriber_proxy.cpp",
                              "*_subscriber_local_live_view_proxy.cpp",
                              "*badge_query_callback_proxy.cpp",
                            ])
  deps = [ ":ans_manager_interface" ]
  defines = []
  if (notification_smart_reminder_supported) {
    defines += [ "NOTIFICATION_SMART_REMINDER_SUPPORTED" ]
    sources += filter_include(output_values, [ "*_call_back_proxy.cpp" ])
  }
  if (build_variant == "root") {
    defines += [ "ANM_SUPPORT_DUMP" ]
  }
  external_deps = [
    "ability_runtime:wantagent_innerkits",
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "hilog:libhilog",
    "i18n:intl_util",
    "image_framework:image_native",
    "ipc:ipc_core",
  ]
  subsystem_name = "notification"
  part_name = "distributed_notification_service"
}

ohos_source_set("ans_callback_stub") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  public_configs = [ ":ans_innerkits_config" ]
  output_values = get_target_outputs(":ans_manager_interface")
  sources = filter_include(output_values,
                            [
                              "*_dialog_callback_stub.cpp",
                              "*_operation_callback_stub.cpp",
                              "*_result_data_synchronizer_stub.cpp",
                              "*_subscriber_stub.cpp",
                              "*_subscriber_local_live_view_stub.cpp",
                              "*badge_query_callback_stub.cpp",
                            ])
  defines = []
  if (notification_smart_reminder_supported) {
    defines += [ "NOTIFICATION_SMART_REMINDER_SUPPORTED" ]
    sources += filter_include(output_values, [ "*_call_back_stub.cpp" ])
  }
  if (build_variant == "root") {
    defines += [ "ANM_SUPPORT_DUMP" ]
  }
  deps = [ ":ans_manager_interface" ]
  external_deps = [
    "ability_runtime:wantagent_innerkits",
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "hilog:libhilog",
    "i18n:intl_util",
    "image_framework:image_native",
    "ipc:ipc_core",
  ]
  subsystem_name = "notification"
  part_name = "distributed_notification_service"
}